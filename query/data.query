PREFIX dataid: <http://dataid.dbpedia.org/ns/core#>
PREFIX dbcv:   <http://dataid.dbpedia.org/ns/cv#>
PREFIX dct:    <http://purl.org/dc/terms/>
PREFIX dcat:   <http://www.w3.org/ns/dcat#>
PREFIX xsd:    <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?file WHERE {
  {
    SELECT ?file WHERE {
      ?dataset dataid:group <https://databus.dbpedia.org/dbpedia/generic> ;
              dataid:artifact ?artifact ;
              dcat:distribution ?distribution .

      ?distribution dataid:compression 'bzip2'^^<http://www.w3.org/2001/XMLSchema#string> ;
                    dataid:formatExtension 'ttl'^^<http://www.w3.org/2001/XMLSchema#string> ;
                    dbcv:lang 'en'^^<http://www.w3.org/2001/XMLSchema#string> ;
                    dct:hasVersion ?version ;
                    dataid:file ?file .
      
      FILTER NOT EXISTS {
        ?otherDataset dataid:group <https://databus.dbpedia.org/dbpedia/generic> ;
                      dataid:artifact ?artifact ;
                      dcat:distribution ?otherDistribution .
        ?otherDistribution dct:hasVersion ?otherVersion .
        FILTER(str(?otherVersion) <= "DUMP_VERSION") .
        FILTER(str(?otherVersion) > str(?version)) .
      }
      FILTER(str(?version) <= "DUMP_VERSION") .
      FILTER(?artifact IN (<https://databus.dbpedia.org/dbpedia/generic/categories>, <https://databus.dbpedia.org/dbpedia/generic/commons-sameas-links>, <https://databus.dbpedia.org/dbpedia/generic/disambiguations>,
        <https://databus.dbpedia.org/dbpedia/generic/external-links>, <https://databus.dbpedia.org/dbpedia/generic/geo-coordinates>, <https://databus.dbpedia.org/dbpedia/generic/infobox-properties>,
        <https://databus.dbpedia.org/dbpedia/generic/infobox-property-definitions>, <https://databus.dbpedia.org/dbpedia/generic/interlanguage-links>, <https://databus.dbpedia.org/dbpedia/generic/labels>,
        <https://databus.dbpedia.org/dbpedia/generic/images>, <https://databus.dbpedia.org/dbpedia/generic/page>, <https://databus.dbpedia.org/dbpedia/generic/persondata>, <https://databus.dbpedia.org/dbpedia/generic/redirects>,
        <https://databus.dbpedia.org/dbpedia/generic/revisions>, <https://databus.dbpedia.org/dbpedia/generic/wikipedia-links>, <https://databus.dbpedia.org/dbpedia/generic/wikilinks>)) .
    }
  } UNION {
    SELECT ?file ?artifact WHERE {
      ?dataset dataid:group <https://databus.dbpedia.org/dbpedia/mappings> ;
              dataid:artifact ?artifact ;
              dcat:distribution ?distribution .

        ?distribution dataid:compression 'bzip2'^^<http://www.w3.org/2001/XMLSchema#string> ;
                    dataid:formatExtension 'ttl'^^<http://www.w3.org/2001/XMLSchema#string> ;
                    dbcv:lang 'en'^^<http://www.w3.org/2001/XMLSchema#string> ;
                    dct:hasVersion ?version ;
                    dataid:file ?file .
      
      FILTER NOT EXISTS {
        ?otherDataset dataid:group <https://databus.dbpedia.org/dbpedia/mappings> ;
                      dataid:artifact ?artifact ;
                      dcat:distribution ?otherDistribution .
        ?otherDistribution dct:hasVersion ?otherVersion .
        FILTER(str(?otherVersion) <= "DUMP_VERSION") .
        FILTER(str(?otherVersion) > str(?version)) .
      }
      FILTER(?artifact != <https://databus.dbpedia.org/dbpedia/mappings/mappingbased-objects-uncleaned>) .
      FILTER(str(?version) <= "DUMP_VERSION") .
    }
  } UNION  {
    SELECT ?file WHERE {
      ?dataset dataid:group <https://databus.dbpedia.org/propan/lhd> ;
              dataid:artifact ?artifact ;
              dcat:distribution ?distribution .
      
      ?distribution dbcv:type ?c0 ;
                    dbcv:lang 'en'^^<http://www.w3.org/2001/XMLSchema#string> ;
                    dct:hasVersion ?version ;
                    dcat:downloadURL ?file .
      
      VALUES ?c0 {
        'extension'^^<http://www.w3.org/2001/XMLSchema#string>
        'core'^^<http://www.w3.org/2001/XMLSchema#string>
      }
      FILTER NOT EXISTS {
        ?otherDataset dataid:group <https://databus.dbpedia.org/propan/lhd> ;
                      dataid:artifact ?artifact ;
                      dcat:distribution ?otherDistribution .
        ?otherDistribution dct:hasVersion ?otherVersion .
        FILTER(str(?otherVersion) <= "DUMP_VERSION") .
        FILTER(str(?otherVersion) > str(?version)) .
      }
      FILTER(str(?version) <= "DUMP_VERSION") .
    }
  } UNION {
    SELECT ?file WHERE {
      ?dataset dataid:group <https://databus.dbpedia.org/vehnem/text> ;
              dataid:artifact ?artifact ;
              dcat:distribution ?distribution .
      
      ?distribution dbcv:lang 'en'^^<http://www.w3.org/2001/XMLSchema#string> ;
                    dct:hasVersion ?version ;
                    dataid:file ?file .
    
        FILTER NOT EXISTS {
        ?otherDataset dataid:group <https://databus.dbpedia.org/vehnem/text> ;
                      dataid:artifact ?artifact ;
                      dcat:distribution ?otherDistribution .
        ?otherDistribution dct:hasVersion ?otherVersion .
        FILTER(str(?otherVersion) <= "DUMP_VERSION") .
        FILTER(str(?otherVersion) > str(?version)) .
      }
      FILTER(str(?version) <= "DUMP_VERSION") .
    }
  } UNION {
    SELECT ?file WHERE {
      ?dataset dataid:group <https://databus.dbpedia.org/dbpedia/transition> ;
              dataid:artifact <https://databus.dbpedia.org/dbpedia/transition/sdtypes> ;
              dcat:distribution ?distribution .
      
      ?distribution dbcv:lang 'en'^^<http://www.w3.org/2001/XMLSchema#string> ;
                    dct:hasVersion ?version ;
                    dataid:file ?file .
    
        FILTER NOT EXISTS {
        ?otherDataset dataid:group <https://databus.dbpedia.org/dbpedia/transition> ;
                      dataid:artifact <https://databus.dbpedia.org/dbpedia/transition/sdtypes> ;
                      dcat:distribution ?otherDistribution .
        ?otherDistribution dct:hasVersion ?otherVersion .
        FILTER(str(?otherVersion) <= "DUMP_VERSION") .
        FILTER(str(?otherVersion) > str(?version)) .
      }
      FILTER(str(?version) <= "DUMP_VERSION") .
    }
  }
}
